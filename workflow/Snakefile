import pandas as pd


configfile: "config/config.yaml"


IDS = [str(i) for i in config["run_ids"]]

samples = pd.read_csv("config/samples.csv", index_col=["protein", "folder"])
PROTEINS = samples.index.unique(level="protein").tolist()
FOLDERS = samples.index.unique(level="folder").tolist()

# cis K18Ac, trans K18Ac and cis + trans K18Ac compared against unmod
plip_types = ["cis", "trans", "all"]
# rmsf all refers to cis + trans K18Ac vs. unmod
rmsf_types = ["cis", "trans", "all", "cis_mut"]


# Load rules
include: "rules/plip_hh.smk"
include: "rules/plip_type_breakdown.smk"
include: "rules/plip_hydrophobic.smk"
include: "rules/peptide_rms.smk"
include: "rules/yf_dists.smk"
include: "rules/vmd_hbonds.smk"


rule all:
    input:
        plip_k18_hh_plots=expand(
            "combined_plots/plip_hh/plip_k18_k18ac_{i}.png", i=plip_types
        ),
        plip_k9_hh_plots=expand(
            "combined_plots/plip_hh/plip_k9_k9me3_{i}.png", i=plip_types
        ),
        plip_r17_hh_plots=expand(
            "combined_plots/plip_hh/plip_r17_{i}.png", i=plip_types
        ),
        plip_k18_breakdown_plots=expand(
            "combined_plots/plip_breakdown/plip_k18_k18ac_{i}_breakdown.png",
            i=plip_types,
        ),
        plip_k9_breakdown_plots=expand(
            "combined_plots/plip_breakdown/plip_k9_k9me3_{i}_breakdown.png",
            i=plip_types,
        ),
        plip_r17_breakdown_plots=expand(
            "combined_plots/plip_breakdown/plip_r17_{i}_breakdown.png", i=plip_types
        ),
        plip_k18_hydrophobic=expand(
            "combined_plots/plip_hydrophobic/plip_k18ac_{i}_key_hydrophobic.png",
            i=["cis", "trans"],
        ),
        pep_rmsf=expand(
            "combined_plots/peptide/pep_backbone_rmsf_k18_k18ac_{i}.png",
            i=rmsf_types,
        ),
        yf_dist_apo_unmod=expand(
            "combined_plots/YF_dists/YF_{i}_k18ac_{k}_violins.png",
            i=["apo", "unmod"],
            k=["cis", "trans", "mut"],
        ),
        yf_dist_cis=expand(
            "combined_plots/YF_dists/YF_k18ac_cis_{i}_violins.png", i=["trans"]
        ),
        vmd_bonds=expand(
            "combined_plots/vmd_hbonds/k18ac_{i}_{k}_heatmap.png",
            i=["cis", "trans"],
            k=["k18ac", "r17"],
        ),
